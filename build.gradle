plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.124'
}

def secrets = new Properties()
def secretsFile = file('secrets.properties')
if (secretsFile.exists()) {
    secretsFile.withInputStream {
        stream -> secrets.load(stream)
    }

    fileTree("secrets").matching {
        include "**/*.properties"
    }.each {
        File file ->
            file.withInputStream {
                stream -> secrets.load(stream)
            }
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

version = "${project.minecraft_version}-${project.mod_version}"
group = 'com.memedream.classicmobs'
base {
    archivesName = "${project.modid}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
compileJava.options.compilerArgs << "-Xlint:all,-classfile,-processing,-deprecation,-this-escape" << "-Werror"

neoForge {
    version = "${project.neo_version}"
    validateAccessTransformers = true

    runs {
        configureEach {
            logLevel = org.slf4j.event.Level.DEBUG
        }

        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', modid
            programArguments.addAll '--username', secrets.getProperty("username") ?: 'Dev', secrets.getProperty("uuid") ? '--uuid' : '', secrets.getProperty("uuid") ?: ''
        }

        server {
            server()
            systemProperty 'neoforge.enabledGameTestNamespaces', modid
            programArgument '--nogui'
        }

        data {
            data()
            programArguments.addAll '--mod', modid, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }
    }

    mods {
        modid {
            sourceSet this.sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name 'Jared\'s Maven' // JEI
        url 'https://maven.blamejared.com/'
    }
    maven {
        name 'Shedaniel\'s Maven' //REI
        url "https://maven.shedaniel.me"
    }
    maven {
        name "TerraformersMC" //EMI
        url "https://maven.terraformersmc.com/"
    }
    maven {
        name 'Curseforge Maven' // Jade
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    //make sure to only pick one of these when testing (switch others to compileOnly)
//	compileOnly "mezz.jei:jei-${project.minecraft_version}-neoforge:${project.jei_version}"
//	compileOnly "me.shedaniel:RoughlyEnoughItems-neoforge:${project.rei_version}"
//	implementation "dev.emi:emi-neoforge:${project.emi_version}+${project.minecraft_version}"

	runtimeOnly "curse.maven:jade-324717:6853386"
}

tasks.named('jar', Jar).configure {
    manifest {
        attributes([
                "Specification-Title"     : ${project.mod_name},
                "Specification-Vendor"    : "Meme Dream",
                "Specification-Version"   : "1",
                "Implementation-Title"    : ${project.mod_name},
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Meme Dream",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
